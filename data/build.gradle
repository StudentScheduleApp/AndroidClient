plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}

android {
    namespace 'com.studentschedulleapp.androidclient'
    compileSdk 34

    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        minSdk 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField 'java.util.Hashtable<String, String>', 'secrets', getSecretProperties()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def getSecretProperties() {
    def devProperties = new Properties()
    try {
        devProperties.load(new FileInputStream(file(getRootDir().getPath() + File.separator + 'secret.properties')))
    } catch (FileNotFoundException e) {
        devProperties = null
        System.out.println('devProperties value is null')
    }
    def prefix = 'new java.util.Hashtable<String, String>(){{ '
    def suffix = '}}'
    def value = ''
    if (devProperties != null) {
        for (d in devProperties) {
            value += String.format('put("%s",%s); ', d.key, "\"" + d.value + "\"")
        }
    }
    return String.format('%s%s%s', prefix, value, suffix)
}

dependencies {

    implementation project(':domain')
    implementation libs.lombok
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.androidx.core.ktx
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}